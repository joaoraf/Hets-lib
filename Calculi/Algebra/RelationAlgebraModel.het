library Calculi/Algebra/RelationAlgebraModel

version 0.2
%author: S. Wölfl
%date: 03-06-2005

%( 
    This library defines some basic higher order concepts from the 
    theory of relation algebras (cf., for example, I. Hodkinson, Atom
    structures of relation algebras, 1995).
)%

from HasCASL/Set get Set
from Calculi/Algebra/RelationAlgebra get RelationAlgebra,
	ExtRelationAlgebraByOrderRelations, RelationAlgebraWithOrderRelations,
	AtomicRelationAlgebra
from Calculi/Algebra/RelationAlgebraSymbolic get CompositionTable
from Calculi/Algebra/BooleanAlgebra get BooleanAlgebraWithCompl


logic HasCASL


%( Part I: Set Algebras of Binary Relations and Algebras of Binary Relations
)% 




spec BinaryRelations[sort Elem] = 
     Set	
then 
     type Relation ::= abs(rep:Set(Elem*Elem))
end


     


spec FullAlgebraOfBinaryRelations[sort DomElem] =
	BinaryRelations[sort DomElem] with sort Relation |-> DomRelation
then %def
     ops
	 0,1,id:DomRelation;
         __cup__,__cap__,__cmps__: DomRelation * DomRelation -> DomRelation;
         compl__,inv__: DomRelation -> DomRelation

     forall r,s:DomRelation; x,y:DomElem
     . rep(0) = emptySet
     . rep(1) = allSet
     . rep(r cup s) = rep(r) union rep(s)
     . rep(r cap s) = rep(r) intersection rep(s)
     . rep(compl(r)) = rep(1) \\ rep(r)
     . (x,y) isIn rep(id) <=> x = y
     . (x,y) isIn rep(r cmps s) <=> exists z:DomElem . (x,z) isIn rep(r) /\ (z,y) isIn rep(s) 
     . (x,y) isIn rep(inv(r)) <=> (y,x) isIn rep(r)

then %implies 
     ops
         __cup__:  DomRelation * DomRelation -> DomRelation, assoc, comm, idem, unit 0;
	 __cap__:  DomRelation * DomRelation -> DomRelation, assoc, comm, idem, unit 1;
         __cmps__: DomRelation * DomRelation -> DomRelation, assoc, unit id;

     forall x:DomRelation
     . compl(compl(x)) = x 
     . inv(inv(x)) = x 
     . inv(id) = id
end



spec SetAlgebraOfBinaryRelations[sort DomElem] =
     FullAlgebraOfBinaryRelations[sort DomElem] 
then 
     sort DomRel < DomRelation
     ops
	 0,1:DomRel;
         __cup__,__cap__: DomRel * DomRel -> DomRel;
         compl__: DomRel -> DomRel
	%% hide sort Relation 
then %implies 
     ops
         __cup__:  DomRel * DomRel -> DomRel, assoc, comm, idem, unit 0;
	 __cap__:  DomRel * DomRel -> DomRel, assoc, comm, idem, unit 1;
     forall x:DomRel
     . compl(compl(x)) = x 
end



spec AlgebraOfBinaryRelations[sort DomElem] =
     SetAlgebraOfBinaryRelations[sort DomElem] 
then  
     ops
	 id:DomRel;
         __cmps__:  DomRel * DomRel -> DomRel;
         inv__: DomRel -> DomRel
end


view SetAlgebraOfBinaryRelations_as_BooleanAlgebraWithCompl:
     BooleanAlgebraWithCompl to SetAlgebraOfBinaryRelations[sort DomElem]
=
     sort Elem |-> DomRel 
end


view AlgebraOfBinaryRelations_as_RelationAlgebra:
     RelationAlgebra to AlgebraOfBinaryRelations[sort DomElem]
=
     sort Rel |-> DomRel
end


%( Part II: Models of Syntatic (Abstract) Relation Algebras 
)%


spec BaseRelationModel =
     BinaryRelations[sort DomElem] with sort Relation |-> DomRelation      
then 
     type DomBaseRel < DomRelation   
%%    forall r:BaseRel . not rep(r) = emptySet
end


spec JEPDBaseRelationModel =
     BaseRelationModel
then
     forall x,y:DomElem; r,s:DomBaseRel
     . exists r:DomBaseRel . (x,y) isIn rep(r)			%(JointlyExhaustive)%
     . not r = s => rep(r) intersection rep(s) = emptySet	%(PairwiseDisjoint)%
end



spec GenerateBooleanAlgebraModel[JEPDBaseRelationModel] = %def
     type DomRel = {x:DomRelation . exists X:Set(DomBaseRel) .  
			(forall y,z:DomElem . (y,z) isIn rep(x) <=> 
		(exists r:DomBaseRel . r isIn X /\ (y,z) isIn rep(r)))}
and
     SetAlgebraOfBinaryRelations[sort Elem]

then %implies
     type DomBaseRel < DomRel
end


spec GenerateRelationAlgebraModel[JEPDBaseRelationModel] = 
     GenerateBooleanAlgebraModel[JEPDBaseRelationModel] 
then
     ops
         id:DomRel;
	 inv__: DomRel -> DomRel;
	 __cmps__: DomRel * DomRel -> DomRel

     forall r,s:DomRel; x,y:DomElem
     . (x,y) isIn rep(id)  <=> x = y
     . (x,y) isIn rep(inv(r)) <=> (y,x) isIn rep(r)
     . (x,y) isIn rep(r cmps s) <=> exists z:DomElem . (x,z) isIn rep(r) /\ (z,y) isIn rep(s)
end


spec GenerateWeakRelationAlgebraModel[JEPDBaseRelationModel] = 
     GenerateBooleanAlgebraModel[JEPDBaseRelationModel] 
then
     ops
         id:DomRel;
	 inv__: DomRel -> DomRel;
	 __wcmps__: DomRel * DomRel -> DomRel

     forall r,s:DomRel; x,y:DomElem
     . (x,y) isIn rep(id)  <=> x = y
     . (x,y) isIn rep(inv(r)) <=> (y,x) isIn rep(r)
     . exists z:DomElem . (x,z) isIn rep(r) /\ (z,y) isIn rep(s) => (x,y) isIn rep(r cmps s)  
end







%( Part III: Representation Concepts for Relation Algebras
)% 


spec RepresentationOfRelationAlgebra [RelationAlgebra] =
     Set 
then
     sort Dom
     op int: Rel -> Set (Dom * Dom)

     forall r,s:Rel;x,y:Dom	
     . not (x,y) isIn int(0)
     . (x,y) isIn int(r cup s) <=> (x,y) isIn int(r) \/ (x,y) isIn int(s)
     . (x,y) isIn int(r cap s) <=> (x,y) isIn int(r) /\ (x,y) isIn int(s)
     . (x,y) isIn int(1) => ((x,y) isIn int(compl(r)) <=> not (x,y) isIn int(r))
     . (x,y) isIn int(id:Rel)  <=> x = y
     . (x,y) isIn int(inv(r)) <=> (y,x) isIn int(r)
     . (x,y) isIn int(r cmps s) <=> (exists z:Dom . (x,z) isIn int(r) /\ (z,y) isIn int(s))
end


spec CompleteRepresentationOfRelationAlgebra [RelationAlgebraWithOrderRelations] =
     RepresentationOfRelationAlgebra [RelationAlgebra] 
then
     forall x,y:Dom
     . (x,y) isIn int(1:Rel) => (exists r:Rel . Atom(r) /\ (x,y) isIn int(r))
end


view CompleteRepresentationOfRelationAlgebra_as_AtomicRelationAlgebra :
     AtomicRelationAlgebra to 
    { CompleteRepresentationOfRelationAlgebra[RelationAlgebraWithOrderRelations] 
	then %def
	sort AtomRel = {r:Rel . Atom(r)}
    }
end





%[
spec GenerateWeakRelationAlgebraModel[JEPDBaseRelationModel] = 
     GenerateBooleanAlgebraModel[JEPDBaseRelationModel] 
then %def
     ops
         id:Relation;
	 inv: Rel -> Relation;
	 __cmps__: Rel * Rel -> Relation

     forall r,s:Rel; x,y:Elem
     . 0 = emptySet:Set(Elem*Elem)
     . 1 = allSet:Set(Elem*Elem)
     . r cup s = r union s
     . r cap s = r intersection s
     . compl(r) = 1 \\ r
     . (x,y) isIn r cmps s <=> exists z:Elem . (x,z) isIn r /\ (z,y) isIn s 
     . (x,y) isIn inv(r) <=> (y,x) isIn r
     . (x,y) isIn id <=> x = y

then %implies 
     ops
         __cup__:  Rel * Rel -> Rel, assoc, comm, idem, unit 0;
	 __cap__:  Rel * Rel -> Rel, assoc, comm, idem, unit 1;  
         __cmps__: Rel * Rel -> Rel, assoc, unit id;
     forall x:Rel
     . compl(compl(x)) = x 
     . inv(inv(x)) = x 

     . (x,y) isIn rep(id)  <=> x = y
     . (x,y) isIn rep(inv(r)) <=> (y,x) isIn rep(r)
     . (x,y) isIn rep(r cmps s) <=> exists z:Elem . (x,z) isIn rep(r) /\ (z,y) isIn rep(s)
then %implies
     type BaseRel < Rel
end
]%



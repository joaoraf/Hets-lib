library Calculi/Algebra/RelationAlgebraSymbolic
version 0.2
%author: T. Mossakowski, K. Lüttich, S. Wölfl
%date: 12-05-2005

%( 
    This library contains specifications that describe how a relation
    algebra can be built from a finite set of base relations and a composition 
    table.
)%


%left_assoc __cup__,__cap__
%prec {__cup__} < {__cmps__} 
%prec {__cmps__} < {__cmpl__} 


from Basic/Numbers get Nat
%% from Basic/StructuredDatatypes get Set
from Calculi/Algebra/Set get Set
from Calculi/Algebra/BooleanAlgebra get 
	BooleanAlgebra,
	AtomicBooleanAlgebra, 	
	ExtBooleanAlgebraByOrderRelations
from Calculi/Algebra/RelationAlgebra get 
	AtomicRelationAlgebra



%(
    If the set of base relations is JEPD, i.e., is the base relations are 
    pairwise disjoint and jointly exhaustive, unions of base relations can be
    represented as sets of base relations.
)%



spec BaseRelations =
     sort BaseRel
end


spec SetRepresentationOfRelations[BaseRelations] = %mono
     { Set[sort BaseRel fit Elem |-> BaseRel] 
	    with Set[BaseRel] |-> Rel,
	            __union__ |-> __cup__, 
             __intersection__ |-> __cap__ }
then %mono
     sort BaseRel < Rel
     ops 
	    0,1 : Rel;
	  compl : Rel -> Rel; 

     forall x:BaseRel; r:Rel
     . x = {x}
     . x eps 1
     . not x eps 0
     . compl(r) = 1 - r

then %implies
     ops
	  __cup__: Rel * Rel -> Rel, assoc, idem, comm, unit 0;
          __cap__: Rel * Rel -> Rel, assoc, idem, comm, unit 1;
 
     forall x,y:BaseRel; r:Rel
	     . not x = y => x cap y = 0		
     . exists z:BaseRel . z eps r
     . r isSubsetOf 1
end



%[
spec GenerateRelations[BaseRelations] = %mono
     SetRepresentationOfRelations[BaseRelations]
end
]%

spec BooleanAlgebraOfRelations =  
     SetRepresentationOfRelations[BaseRelations]
end


view BooleanAlgebraOfRelations_as_AtomicBooleanAlgebra :
     AtomicBooleanAlgebra to BooleanAlgebraOfRelations
= 
     Elem |-> Rel, AtomElem |-> BaseRel
end



spec CompositionTable =
     sorts BaseRel < Rel
     ops 
         id	 : BaseRel;	 	 
          1      : Rel;
	  __cup__: Rel * Rel -> Rel, assoc, idem, comm, unit 1;
         __cmps__: BaseRel * BaseRel -> Rel
     forall x:BaseRel
     . x cmps id = x
     . id cmps x = x
end



spec InvolutionTable =
     sorts BaseRel < Rel
     ops 
          id     : BaseRel;
	  inv__ : BaseRel -> BaseRel
     forall x:BaseRel
     . inv(id) = id
     . inv(inv(x)) = x
end



spec GenerateRelationAlgebra[BooleanAlgebraOfRelations][CompositionTable][InvolutionTable] = %mono
     ops 
	  id	  : Rel;
          inv__  : Rel -> Rel;
	  __cmps__: Rel * Rel -> Rel       

     forall x,y:BaseRel; u,v:Rel
     . inv(0) = 0
     . inv(u cup x) = inv(u) cup inv(x)
     . x cmps 0 = 0 /\ 0 cmps x = 0
     . (u cup x) cmps (v cup y) =
	  (u cmps v) cup (u cmps y) cup (x cmps v) cup (x cmps y)      

then %implies
     op __cmps__: Rel * Rel -> Rel, assoc, unit id;       
end



spec GeneratedRelationAlgebra = 
     GenerateRelationAlgebra[BooleanAlgebraOfRelations][CompositionTable][InvolutionTable]
end


spec ExtGeneratedRelationAlgebra[GeneratedRelationAlgebra] = %def
     ExtBooleanAlgebraByOrderRelations[BooleanAlgebra with sort Elem |-> Rel]
then %implies
     forall x,y:Rel
     . x < y  <=> x isSubsetOf y /\ not x = y
     . x <= y <=> x isSubsetOf y
     . x > y  <=> y isSubsetOf x /\ not x = y
     . x >= y <=> y isSubsetOf x		
end


spec RichGeneratedRelationAlgebra =  ExtGeneratedRelationAlgebra[GeneratedRelationAlgebra]
end


view GeneratedRelationAlgebra_as_AtomicRelationAlgebra:
     AtomicRelationAlgebra 
to
     RichGeneratedRelationAlgebra  
=
     Rel |-> Rel, AtomRel |-> BaseRel
end 


library Calculi/Space/TopologicalCalculi


from Calculi/Space/RCC5 get RCC5,GenerateOpenDiscModelOfRCC5
from Calculi/Space/RCC8 get RCC8,GenerateOpenDiscModelOfRCC8
from HasCASL/MetricSpaces get MetricSpace
from Calculi/Time/IntervalAlgebraLin get IntervalAlgebraLin |-> AllenIA
from Calculi/Time/IntervalAlgebraLin get GenerateAllenModelOfIntervalAlgebraLin |-> GenerateAllenModelOfIA
from Calculi/Time/LinearFlowOfTime get DnsLinFlowOfTimeSEnd

view RCC5_to_RCC8 :
     RCC5 
to   { RCC8 
	then %def
     	ops dr,pp,ppi:Rel
     	. dr = dc cup ec
     	. pp = tpp cup ntpp
     	. ppi = tppi cup ntppi
      }
=    sort BaseRel |-> Rel 
end

view RCC8_to_AllenIA :
     RCC8 
to   { AllenIA then %def
       ops dc,ec,po,tpp,ntpp,tppi,ntppi,eq: Rel
       . dc = b cup bi
       . ec = m cup mi
       . po = o cup oi
       . tpp = s cup f
       . tppi = si cup fi
       . ntpp = d
       . ntppi = di
       . eq = e 
	}
=    sort BaseRel |-> Rel 
end


logic HasCASL

view MetricSpace_induces_OpenDiscModelOfRCC5 :
     { RCC5 hide ops __+__,__-__,{},{__}    
	hide preds __eps__,__isSubsetOf__,isNonEmpty } 
to 
    GenerateOpenDiscModelOfRCC5[MetricSpace]
=   
    ops pp|->ppRel, ppi|->ppiRel, po|->poRel, dr|->drRel, eq|->eqRel, 
        id |-> eqRel
end


view MetricSpace_induces_OpenDiscModelOfRCC8 :
     { RCC8 hide ops __+__,__-__,{},{__}    
	hide preds __eps__,__isSubsetOf__,isNonEmpty } %% <- Das muss man verbessern, weil wir dann zu viel Theorie verlieren
to 
    GenerateOpenDiscModelOfRCC8[MetricSpace]
=   
    ops dc|->dcRel, ec|->ecRel, po|->poRel, tpp|->tppRel, ntpp|->ntppRel, 
	tppi|->tppiRel, ntppi|->ntppiRel, eq|->eqRel, 
        id |-> eqRel
end

view DnsLinFlowOfTimeSEnd_induces_AllenModelOfIntervalAlgebraLin :
     { AllenIA hide ops __+__,__-__,{},{__}    
	hide preds __eps__,__isSubsetOf__,isNonEmpty } 
to 
    GenerateAllenModelOfIA[DnsLinFlowOfTimeSEnd]
=   
    ops b|->bRel, m|->mRel, o|->oRel, d|->dRel, s|->sRel, f|->fRel, e|->eRel, 
	bi|->biRel, mi|->miRel, oi|->oiRel, di|-> diRel, si |-> siRel, fi|->fiRel,
        id |-> eRel
end

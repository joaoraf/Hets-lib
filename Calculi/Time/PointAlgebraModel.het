library Calculi/Time/PointAlgebraModel

version 0.1
%author: S. Wölfl
%date: 15-02-2005

%( 
    This library defines the model classes for the relation algebras in
    
	Calculi/Time/PointAlgebra

)%


from Basic/RelationsAndOrders get BooleanAlgebra
from Calculi/Algebra/RelationAlgebraModel get  Relation,
	BaseRelationModel,JEPDBaseRelationModel,
	GenerateModelFromBaseRelationModel,
	AlgebraOfBinaryRelations
from Calculi/Time/LinearFlowOfTime get LinFlowOfTime,DnsLinFlowOfTimeSEnd
from Calculi/Time/PointAlgebraCompositionTables get CompositionTableOfPointAlgebraLin
from Calculi/Time/PointAlgebraSymbolic get PointAlgebraLin

logic HasCASL


spec BaseRelationModelOfPointAlgebraLin[LinFlowOfTime] = %def 
     Relation[sort Instant] 
then  
     ops preRel,idRel,sucRel:Relation 
     type BaseRel ::= preRel | idRel | sucRel
     forall x,y:Instant
     . (x,y) isIn rep(preRel) <=> x pre y
     . (x,y) isIn rep(idRel) <=> x = y
     . (x,y) isIn rep(sucRel) <=> y pre x
end


view BaseRelationModelOfPointAlgebraLin_as_JEPDBaseRelationModel:
     JEPDBaseRelationModel[sort Instant] 
to 
     BaseRelationModelOfPointAlgebraLin[LinFlowOfTime]
end


spec GeneratePreModelOfPointAlgebraLin[LinFlowOfTime] = %def
      GenerateModelFromBaseRelationModel[BaseRelationModelOfPointAlgebraLin[LinFlowOfTime]
	fit sort Elem |-> Instant]
then %implies
     ops  idRel: Rel;
          conv: BaseRel -> BaseRel;
          conv: Rel -> Rel
end


spec GenerateModelOfPointAlgebraLin[DnsLinFlowOfTimeSEnd] = %def
     GeneratePreModelOfPointAlgebraLin[LinFlowOfTime]
then %implies     
     op  __cmps__:  Rel * Rel -> Rel;
	 __cmps__: BaseRel * BaseRel -> Rel;  %% <- Das sollte eigentlich klar sein?
end


view DsnLinFlowOfTimeSEnd_induces_AlgebraOfBinaryRelations :
    AlgebraOfBinaryRelations[sort Elem] 
to 
    GenerateModelOfPointAlgebraLin[DnsLinFlowOfTimeSEnd]
=
    sort Elem |-> Instant , op id |-> idRel 
end



view DsnLinFlowOfTimeSEnd_induces_ModelOfPointAlgebraLin :
     { PointAlgebraLin hide ops __+__,__-__,{},{__}    %% <- Das muss man verbessern, 
	hide preds __eps__,__isSubsetOf__,isNonEmpty } %%    weil wir dann zu viel Theorie verlieren
to 
    GenerateModelOfPointAlgebraLin[DnsLinFlowOfTimeSEnd]
=   
    ops pre |-> preRel, id |-> idRel, suc |-> sucRel
end








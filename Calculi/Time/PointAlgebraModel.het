library Calculi/Time/PointAlgebraModel

version 0.1
%author: S. Wölfl
%date: 15-02-2005

%( 
    This library defines the model classes for the relation algebras in
    
	Calculi/Time/PointAlgebra

)%


from Calculi/Algebra/RelationAlgebraModel get Relation,
	GeneratePreModelFromBaseRelationModel
from Calculi/Time/LinearFlowOfTime get LinFlowOfTime,DnsLinFlowOfTimeSEnd



logic HasCASL


spec BaseRelationModelOfPointAlgebraLin[LinFlowOfTime] = %def 
     Relation[sort Instant] 
then  
     ops preRel,idRel,sucRel:Relation 
     type BaseRel ::= preRel | idRel | sucRel
     forall x,y:Instant
     . (x,y) isIn rep(preRel) <=> x pre y
     . (x,y) isIn rep(idRel) <=> x = y
     . (x,y) isIn rep(sucRel) <=> y pre x
end


spec GeneratePreModelOfPointAlgebraLin[LinFlowOfTime] = %def
      GeneratePreModelFromBaseRelationModel
	[BaseRelationModelOfPointAlgebraLin[LinFlowOfTime]
      fit sort Elem |-> Instant]
then %implies
     ops  idRel: Rel;
          conv: BaseRel -> BaseRel;
          conv: Rel -> Rel
end


spec GenerateModelOfPointAlgebraLin[DnsLinFlowOfTimeSEnd] = %def
     GeneratePreModelOfPointAlgebraLin[LinFlowOfTime]
then %implies     
     op  __cmps__:  Rel * Rel -> Rel;
	 __cmps__: BaseRel * BaseRel -> Rel;  %% <- Das sollte eigentlich klar sein?
end









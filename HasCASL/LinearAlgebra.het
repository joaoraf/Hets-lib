library HasCASL/LinearAlgebra
version 0.1

%author: E. Schulz
%date: 18-09-2008

logic HasCASL

from HasCASL/Reals get
     Reals, FieldWithValuation, RealFunctions,
     OrderedReals, OrderedReals_as_FieldWithValuation
from HasCASL/Algebra get
     ExtInverse, AbelianGroup, Field

spec AdditiveAbelianGroup_aux =
ExtInverse[AbelianGroup] with
  Elem |-> Space, __ * __ |-> __ + __, e |-> 0, __ / __ |-> __ - __, inv |-> -__
end

spec VectorSpace[Field] = AdditiveAbelianGroup_aux
then
     op  __ * __: Elem * Space -> Space

     forall s,r:Elem; x,y:Space

     . (r * s) * x = r * (s * x)              %(mix_assoc)%
     . (r + s) * x = (r * x) + (s * x)        %(distr_Field)%
     . r * (x + y) = (r * x) + (r * y)        %(distr_Space)%
end

%( Definitional extension: colin-pred )%
spec ExtVectorSpace[VectorSpace[Field]] = %def
     preds colin:Space*Space;

     var x,y:Space
     . colin(x,y) <=> (y=0 \/ exists r:Elem. x = r*y) %(colin_def)%


then %implies
     var x,y:Space; r:Elem
     %{ colin props }%
     . colin(x,x) %(colin reflexivity)%
     . colin(x,y) => colin(y,x) %(colin symmetry)%
     . x = r * y => colin(x,y)  %(simple colin condition)%
end

spec NormedSpace[FieldWithValuation] =
VectorSpace[FieldWithValuation]
then
     op ||__|| : Space -> Real 
     forall a:Elem ; v,w: Space   
     . ||a * v|| = val(a) * ||v||	%(homogen)%
     . ||v + w|| <= ||v|| + ||w||      	%(triangle)%
     . ||v|| = 0 <=> v = 0  	 	%(definit)%
end

spec EuclideanVectorSpace = 
     VectorSpace[Reals fit Elem |-> Real]
then
     op __*__ : Space * Space -> Real   %% (bilinear form)

     forall a:Real ; v,v',w: Space
     . (v+v') * w = (v * w) + (v' * w)  %(distributiv)%
     . (a*v) * w = a * (v * w) 		%(homogen)%
     . v * w  = w * v   		%(commutativ)%
     . v * v = 0 =>  v = 0		%(pos_definit)%

then %implies
     forall a:Real ; v,v',w: Space
     . w * (v+v') = (w * v) + (w * v')  %(right_distributiv)%
     . v * (a*w) = a * (v * w) 		%(right_homogen)%
end

%( Definitional extension of unary inv to binary inv (= /) )%
spec ExtEuclideanVectorSpace[EuclideanVectorSpace] given RealFunctions
= %def
ExtVectorSpace[EuclideanVectorSpace fit Elem |-> Real] then 
     ops  sqr(x:Space):RealPos = x*x as RealPos; %(sqr_def)%
	  ||__|| (x:Space): Real = sqrt(sqr(x)); %(norm_from_scalar_prod_def)%

     pred orth:Space*Space

     vars x,y:Space
     . orth(x,y) <=> x*y=0 %(orthogonal_def)%

then %implies
     vars x,y,z:Space; r:Real

     %{ orth props }%
     . orth(x,y) => orth(y,x) %(orth symmetry)%

     %{ mixed props }%
     . colin(x,y) /\ orth(y,z) => orth(x,z) %(colin orth transitivity)%
end

view EuclideanSpaceIsNormedSpace:
     NormedSpace[view OrderedReals_as_FieldWithValuation]
     to ExtEuclideanVectorSpace[EuclideanVectorSpace]
end

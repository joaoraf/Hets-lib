library HasCASL/Real3D/RigidBodyMechanics
version 0.2

%author: E. Schulz
%date: 13-11-2008

logic HasCASL

from HasCASL/Real3D/Basics get AffineRealSpace3DWithSets


%[%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%                                                                  %%
%%                          Mechanics for Flange                    %%
%%                                                                  %%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%]%

%[
  The goal here is to express the forces on the cover of a flange which
  result from the pressure in the tube of a flange and from the screws
  which connect the flange with the cover.
  Ideally one would like to express the forces seperately for the local
  components, e.g., if we have a screw at point x which "connects" the
  faces A, B which are put "flat" together (the faces are parallel), then
  we can obtain a maximal force on A at point x which is directed towards B
  and whose absolute value equals the internal traction of the connecting
  screw. Then we would define the force on an object as the sum of all local
  forces. Then we would state that the resulting force of the cover has to
  have a positive value in the direction towards the flange.
]%

spec RigidBodyMechanics = AffineRealSpace3DWithSets
then
     ops %[ The law of the resulting Force on a system of mass points ]%
	 ResultingForce(Fi:List Vector):Vector = veclistop Fi; %(resulting force)%
	 %[ pressure and force ]%
	 PressureForce(p:Real;A:Vector):Vector = p * A; %(pressure force)%
end

spec Screw = AffineRealSpace3DWithSets
then
     %[ a screw consists of a drawing of the screw and annotations which
        contain the material and physical properties ]%
     type screw

     ops
     %[ the maximal momentum to be used when tighten the screw
     screw constant, depends on material, radius and
     angle of inclination of screw thread ]%
     ScrewMomentum : screw -> Real;

     %[ this function describes the traction of a screw when tightened
        by a given momentum ]%
     ScrewForceForMomentum: screw * Real -> Real;

     %[ this gives us the normed direction in which the screw works on the material ]%
     ScrewWorkDirection: screw -> Vector;

     %[ assuming we use the maximum momentum to tighten the screw, this is the
        resulting force on the tightened material ]%
     ScrewForce(s:screw):Vector =
     ScrewForceForMomentum(s, ScrewMomentum(s)) * ScrewWorkDirection(s); %(screw force)%


     %% Available screws (coming from database):
     Scr1:screw;
     Scr2:screw;
     %% ...
     ScrN:screw;


     %% Properties of available screws (coming from database): 
     . ScrewMomentum(Scr1) = 1
     . ScrewMomentum(Scr2) = 2
     . ScrewMomentum(ScrN) = 14
end


spec FlangeConfig[Screw] =
     %% the normed axis of the flange
     ops axis:Vector;
     %% the maximal pressure
	 p:Real;

         %[ the neccessary pressure to seal two plates such that no gas
            or fluid can escape from the interior
            material constant, probably dependent on the pressure of the content ]%
	 SealingPressure : Real -> Real;

         %[ the inner and outer radius of the flange ]%
	 innerRadius : Real;
	 outerRadius : Real;

	 %[ a collection of the screws used in this flange ]%
	 screws: List screw;

end

spec FlangeMechanics[FlangeConfig[Screw]] = RigidBodyMechanics
then
     %[ VERIFICATION CONDITION for the flange:
        the resulting force of the forces sum of screw forces, sealing force
        and pressure force has to have a positive value in opposite direction
        of the axis of the flange ]%
     . let innerSurface = Pi * innerRadius * innerRadius;
           outerSurface = Pi * outerRadius * outerRadius;
	   sealingSurface = (outerSurface - innerSurface) * axis;
	   activePressureSurface = innerSurface * axis;
	   sealingForce = PressureForce(SealingPressure(p), sealingSurface);
	   actPrForce = PressureForce(p, activePressureSurface)
       in ResultingForce(sealingForce :: actPrForce :: map ScrewForce screws)
	    * axis < 0

end


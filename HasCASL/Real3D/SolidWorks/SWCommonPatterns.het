library HasCASL/Real3D/SolidWorks/SWCommonPatterns
version 0.2

%author: E. Schulz
%date: 12-12-2008

logic HasCASL

from HasCASL/Real3D/Geometry get PlaneProps, Cylinder
from HasCASL/Real3D/SolidWorks/SolidWorks
     get SolidWorksWithSemantics

%[%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%                                                                  %%
%%                       The SolidWorks common patterns             %%
%%                                                                  %%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%]%

spec SolidWorksPlane_is_AffinePlane = 
     PlaneProps and SolidWorksWithSemantics then
     var plane:SWPlane
     . i(plane) = Plane(SpacePoint(plane),NormalVector(plane)); %(plane is plane)% %implied

end

%%view  SolidWorksPlane_is_AffinePlane: PlaneProps to
%%     {SolidWorksWithSemantics then 
%%      op Plane(p:Point;v:Vector): PointSet = 
%%      i(SWPlane(p,v,0))
%%      }
%%

%%%%%%%%%%%%% semantical equality by theorem %%%%%%%%%%%%%

%% this version has the advantage over the view-version not to have
%% to give a mapping for all visible signature elements in AffineObjects!!

spec SolidWorksCylByArcExtrusion_is_AffineCylinder = 
     SolidWorksPlane_is_AffinePlane and Cylinder then

     ops height:RealPos;
	 center,boundarypoint:Point;
	 plane:SWPlane; %(real plane)%
         arc:SWArc=SWArc(center,boundarypoint,boundarypoint); %(def of given arc)%
	 x1,x2,x3:Real; %(nonused reals for extrusion)%
	 b1,b2,b3,b4,b5:Logical; %(nonused booleans for extrusion)%

	 offset:Point = center; %(cylinder offset)%
	 radius:RealPos = ||vec(center,boundarypoint)||; %(cylinder radius)%
	 axis:VectorStar = VWithLength(NormalVector(plane),height); %(cylinder axis)%

     %{constraints for the given parameters}%
     . height > 0; %(real extrusion)% %% follows from type RealPos
     . not (center = boundarypoint); %(real arc)%
     %% follows from type of axis and axis_def!

     . let p = i(plane)
       in
       (center isIn p) /\ (boundarypoint isIn p) %(the arc is wellformed)%


     %{The main theorem}%
     . i(SWExtrusion(SWSketch([arc],plane),height)) %%,x1,b1,b2,x2,x3,b3,b4,b5))
	  = Cylinder(offset, radius, axis) %(extrusion is cylinder)% %implied

end


%%%%%%%%%%%%% semantical equality by view %%%%%%%%%%%%%

spec SolidWorksCylByArcExtrusion = SolidWorksPlane_is_AffinePlane then
     op SWCylinder(center, boundarypoint:Point; axis:VectorStar):SWFeature
	  = let plane = SWPlane(center, axis, V(0,0,0));
		arc = SWArc(center,boundarypoint,boundarypoint);
		height = ||axis||
%%		x1 = 0:Real;
%%		b = false
	    in
	    SWExtrusion
	       (SWSketch([arc],plane),height); %%,x1,b,b,x1,x1,b,b,b));
						 %(def of SWCylinder)%
end

view SWCylByAE_IsCylinder: Cylinder to 
     { SolidWorksCylByArcExtrusion
       then
       op Cylinder(offset:Point;r:RealPos;axis:VectorStar):PointSet
	  = let boundary = \p:Point . let v = vec(offset, p) in orth(v,axis) /\ ||v|| = r;
	        boundarypoint = choose(boundary)
       in
       i(SWCylinder(offset, boundarypoint, axis)) %(affine cylinder constructible in SW)%
       %% every affine cylinder can be produced in SolidWorks
     }

library piotr_short
%% Example provided by Piotr Hoffman

spec Nat =
  free type Nat ::= 0 | suc(Nat)
end

spec TotalOrder[sort Elem] =
  pred __<__ : Elem * Elem
  forall x,y,z : Elem
  . not x<x
  . x<y /\ y<z => x<z
  . x<y \/ y<x
end

spec List[sort Elem] =
  free type List ::= nil | __::__(Elem;List)
end

spec ListWithMost[sort Elem] =
   List[sort Elem] then
      local
         op leave_only : List * Elem -> List
         pred longer_or_eq : List * List

         vars e, e' : Elem; l, l' : List
         
         axioms
           leave_only(nil, e) = nil;
           leave_only(e::l, e') = 
               e::leave_only(l, e') when e=e' else leave_only(l, e');
               
           longer_or_eq(l, nil);
           longer_or_eq(e::l, e'::l') <=> longer_or_eq(l, l');
           not longer_or_eq(nil, e::l)
      within
         op most : List ->? Elem
         axiom
            forall l : List; e : Elem . not l=nil =>
               longer_or_eq( leave_only(l, most(l)), leave_only(l, e))
end

%% Our goal: to define the 'most' operation assuming a total order on
%% the type of list elements

unit spec GOAL = 
   TotalOrder[sort Elem] * List[sort Elem] -> ListWithMost[sort Elem]
end

%%***************************************************************************

spec Pair[sort Elem1][sort Elem2] =
  free type Pair[Elem1,Elem2] ::= pair( p1 : Elem1; p2 : Elem2)
end


%% First a CounterDict without the special 'incNum' operation.
%% We have the following ops:
%% - empty - all elements have count zero
%% - inc - increment the count of the given element by one
%% - dec - decrement the count of the given element by one
%% - remove - set the count of the given element to zero
%% - num - return the count of the given element

spec CounterDict[sort Elem] =
  Nat then
    sort Dict;
    ops inc, dec, remove : Dict * Elem -> Dict;
        num : Dict * Elem -> Nat;
        empty : Dict
        
    %% axioms ...
end

spec CounterDictWith_IncNum[sort Elem] =
  CounterDict[sort Elem] and 
  Pair[sort Dict][sort Nat] then
  
    op incNum : Dict * Elem -> Pair[Dict,Nat]
    axiom
      forall d : Dict; e : Elem . incNum(d, e) = pair(inc(d, e), num(d, e))
end

unit spec MakePair =
  {sort Elem1} * {sort Elem2} -> Pair[sort Elem1][sort Elem2]
end


arch spec MAIN =
  units
    MakePair : MakePair;

    N : Nat;

    MakeDict :   Nat * 
		 TotalOrder[sort Elem] * 
		 Pair[sort Elem][sort Nat] 
		 ->
    		 CounterDictWith_IncNum[sort Elem];

    DictToListMost :  CounterDictWith_IncNum[sort Elem] * 
			List[sort Elem] 
			-> 
    			ListWithMost[sort Elem]	
  result
    lambda TO : TotalOrder[sort Elem]; 
	     L : List[sort Elem] .
    N %[  DictToListMost[ MakeDict[N][TO][MakePair[TO][N]] ][L]
       reveal Elem, List, __::__, nil, most, __<__
       
      %[ : GOAL ]%
end

%%***************************************************************************

arch spec DictToListMost =
  units
    EnlargeSort : EnlargeSort;

    MakePair : MakePair;

    ExtDictToListMost : CounterDictWith_IncNum[sort Elem] *
    			List[sort Elem] *
  			EnlargeSort[sort Elem] * 
  			Pair[sort Enlarged][sort Nat]
			->
			ListWithMost[sort Elem]
  result
    lambda CDW_IN : CounterDictWith_IncNum[sort Elem];
    	   L : List[sort Elem] .
    	   	local
    	   	  ES = EnlargeSort[ L ];
    	   	  Pair = MakePair[ ES fit Elem1 |-> Enlarged ][ N ];
    	   	within
    	   	  ExtDictToListMost[CDW_IN][L][ES][Pair]
    	   	  reveal Elem, List, __::__, nil, most
    	   	  
    	%[ : CounterDictWith_IncNum[sort Elem] * 
			List[sort Elem] -> 
    			ListWithMost[sort Elem]	
        ]%
end

%%************************************************************************

spec BinTree[sort Elem] =
  free type Tree[Elem] ::= emptyTree | 
  			consTree( left, right :? Tree[Elem]; key :? Elem)
end

spec BSTTree[TotalOrder[sort Elem]] =
  BinTree[Pair[sort Elem][Nat] fit Elem |-> Pair[Elem,Nat]] then
  
    local
      ExtBinTree[Pair[sort Elem][Nat] fit Elem |-> Pair[Elem,Nat]] then
      		
        pred isBST(t : Tree[Pair[Elem,Nat]]) <=> 
        	forall e : Pair[Elem,Nat]; s : Tree[Pair[Elem,Nat]] .
        	subTree(s,t) =>
      		  (elem(e,left(t)) => p1(e) < p1(key(t))) /\
      		  (elem(e,right(t)) => p1(key(t)) < p1(e) );
      		  
      	     nonNull(t : Tree[Pair[Elem,Nat]]) <=> forall e : Pair[Elem,Nat] .
      	     				elem(e,t) => p2(e) > 0		
    within
      sort BST = { t : Tree[Pair[Elem,Nat]] . isBST(t) /\ nonNull(t) }
end


%% specs for building 'num' (and also 'empty')

spec BSTNumEmpty[TotalOrder[sort Elem]] =
  BSTTree[TotalOrder[sort Elem]] and
  CounterDictWith_IncNum[sort Elem] with Dict |-> Tree[Pair[Elem,Nat]]
  			reveal num, empty then
  			
    op num : BST * Elem -> Nat;
       empty : BST
  hide num : Tree[Pair[Elem,Nat]] * Elem -> Nat ,
       empty : Tree[Pair[Elem,Nat]]
end

unit spec AddBSTNumEmptyToTree =
  BinTree[Pair[sort Elem][Nat] fit Elem |-> Pair[Elem,Nat] ] * 
  TotalOrder[sort Elem] 
  -> BSTNumEmpty[TotalOrder[sort Elem]]
end

arch spec MakeDict =
  units
    MakeBinTree : { sort Elem } -> BinTree[sort Elem];
    N : Nat;
    MakePair : MakePair;
    AddBSTNumEmptyToTree : AddBSTNumEmptyToTree;
    AddBSTIncNumToTree : AddBSTIncNumToTree;
    AddBSTIncToIncNumTree : AddBSTIncToIncNumTree;
    AddBSTRemoveToTree : AddBSTRemoveToTree;
    AddBSTDecToRemoveTree : AddBSTDecToRemoveTree;
  result
    lambda 	N : Nat; 
    		TO : TotalOrder[sort Elem]; 
    		PDN : Pair[sort Elem][sort Nat]
		.    		
		local
	  	  Pair = MakePair[ TO ][ Nat ];
	  	  Tree = MakeBinTree[ Pair fit Elem |-> Pair[Elem,Nat] ];
		  NumEmpty = AddBSTNumEmptyToTree[ Tree  ][ TO ];
		  IncNum = AddBSTIncNumToTree[ Tree ][ TO ][ PDN ];
		  Inc = AddBSTIncToIncNumTree[ IncNum ];
		  Remove = AddBSTRemoveToTree[ Tree ][ TO ];
		  Dec = AddBSTDecToRemoveTree[ Remove ];
		within
		  {NumEmpty and Inc and Dec
			hide
		  Pair[Elem,Nat]}
		  
	    %[ : Nat * 
	    	 TotalOrder[sort Elem] * 
	    	 Pair[sort Elem][sort Nat ] 
	    	 ->
    	    	 CounterDictWith_IncNum[sort Elem];
            ]%
end


%%******************************************************************************

spec AddBSTNumToTree =
  BinTree[Pair[sort Elem][Nat] fit Elem |-> Pair[Elem,Nat] ] and 
  TotalOrder[sort Elem] then
  
    BSTTree[ Pair[sort Elem][Nat] fit Elem |-> Pair[Elem,Nat]  ] then
  	
      ops num : BST * Elem -> Nat;
          empty : BST
      vars e : Elem; p : Pair[Elem,Nat]; l, r : BST;
      axioms
        empty = emptyTree;
        num(emptyTree as BST, e) = 0;
        num(consTree(l,r,p) as BST, e) =  
        	p2(p) 
        	     when e=p1(p) else
        	( num(l,e) when e<p1(p) else num(r,e))
end

